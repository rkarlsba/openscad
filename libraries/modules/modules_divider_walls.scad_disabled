function calculateSeparators(seperator_config) = 
  is_string(seperator_config) ? [] : 
  is_list(seperator_config) ? seperator_config : [];

function calculateSeparatorsv2(
                  seperator_config, 
                  length,
                  height,
                  wall_thickness = 0,
                  bend_position = 0,
                  bend_angle = 0,
                  bend_separation = 0,
                  cut_depth = 0) = 
  is_string(seperator_config) 
    ? let(seps = [for (s = split(seperator_config, "|")) csv_parse(s)]) // takes part of an array
      [for (i = [0:len(seps)-1]) [
          is_list(seps[i]) && len(seps[i]) >= 1 ? seps[i][0] : 0,               //0 position
          length,                                                   //1 length
          height,                                                   //2 height
          is_list(seps[i]) && len(seps[i]) >= 6 ? seps[i][5] : wall_thickness,             //3 wall_thickness
          bend_position,                                                      //4 bend_position
          is_list(seps[i]) && len(seps[i]) >= 2 ? seps[i][1] : bend_separation,           //5 bend_separation
          is_list(seps[i]) && len(seps[i]) >= 3 ? seps[i][2] : bend_angle*(i%2==1?1:-1),  //6 bend_angle
          is_list(seps[i]) && len(seps[i]) >= 4 ? seps[i][3] : cut_depth,                 //7 wall_cutout_depth
          is_list(seps[i]) && len(seps[i]) >= 5 ? seps[i][4] : 0                          //8 wall_cutout_width
        ]]
    : (is_list(seperator_config) && len(seperator_config) > 0) 
      ? [for (i = [0:len(seperator_config)-1])[
          seperator_config[i],       //0 position
          length,                    //1 length      
          height,                    //2 height
          wall_thickness,            //3 wall_thickness
          bend_position,             //4 bend_position
          bend_separation,           //5 bend_separation
          bend_angle*(i%2==1?1:-1),  //6 bend_angle
          cut_depth,                 //7 wall_cutout_depth
          0                          //8 wall_cutout_width
          ]]
      : [];

module separators(  
  length,
  height,
  wall_thickness = 0,
  bend_position = 0,
  bend_angle = 0,
  bend_separation = 0,
  cut_depth = 0,
  seperator_config = [])
{
sepConfigs = calculateSeparatorsv2(
    seperator_config = seperator_config, 
    length = length,
    height = height,
    wall_thickness = wall_thickness,
    bend_position = bend_position,
    bend_angle = bend_angle,
    bend_separation = bend_separation,
    cut_depth = cut_depth);
  echo("separators",sepConfigs=sepConfigs);
 
  if(is_list(sepConfigs) && len(sepConfigs) > 0){
    for (i=[0:len(sepConfigs)-1]) {
     sepCfg = sepConfigs[i];
     translate([sepCfg[0]-sepCfg[3]/2,0,0])
     bentWall(
       length=sepCfg[1],
       bendPosition=sepCfg[4],
       bendAngle=sepCfg[6],
       separation=sepCfg[5],
       lowerBendRadius=sepCfg[5]/2,
       upperBendRadius=sepCfg[5]/2,
       height=sepCfg[2],
       wall_cutout_depth = sepCfg[7],
       wall_cutout_width = sepCfg[8],
       thickness=sepCfg[3]);
    }
  }
}

module separatorsv1(  
  length,
  height,
  wall_thickness = 0,
  bend_position = 0,
  bend_angle = 0,
  bend_separation = 0,
  cut_depth = 0,
  seperator_config = [])
{
  if(is_string(seperator_config))
  {
    //Non custom components
    separators = split(seperator_config, "|");
    for (i =[0:1:len(separators)-1])
    {
      sep = csv_parse(separators[i]);
      if(is_list(sep) && len(sep)>0)
      {
        translate([sep[0]-wall_thickness/2,0,0])
        bentWall(
          length=length,
          bendPosition=bend_position,
          separation=len(sep) >= 2 ? sep[1] : bend_separation,
          bendAngle=len(sep) >= 3 ? sep[2] : bend_angle*(i%2==1?1:-1),
          height=height,
          wall_cutout_depth = len(sep) >= 4 ? sep[3] : cut_depth,
          wall_cutout_width = len(sep) >= 5 ? sep[4] : 0,
          thickness=len(sep) >= 6 ? sep[5] : wall_thickness);
        }
     }
  }
  else if(is_list(seperator_config) && len(seperator_config) > 0){
    for (i=[0:len(seperator_config)-1]) {
     translate([seperator_config[i]-wall_thickness/2,0,0])
     bentWall(
       length=length,
       bendPosition=bend_position,
       bendAngle=bend_angle*(i%2==1?1:-1),
       separation=bend_separation,
       lowerBendRadius=bend_separation/2,
       upperBendRadius=bend_separation/2,
       height=height,
       wall_cutout_depth = cut_depth,
       thickness=wall_thickness);
    }
  }
}